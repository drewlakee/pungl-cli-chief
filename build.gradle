import java.nio.file.Files

plugins {
    id 'java'
}

group 'drewlakee.pungl'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.22'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    archiveBaseName = "pungl"

    manifest {
        attributes "Main-Class" : "Main",
                   "Project-Version" : project.getVersion(),
                   "Java-Version" : java.targetCompatibility
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task alias {
    dependsOn build
    description = "Copy jar to specific directory and add app's alias to PATH"

    doLast {
        if (!System.getProperty("os.name").equals("Linux")) {
            System.err.println "Operation System is not a Linux."
            return
        }

        // create $HOME/bin directory, if doesn't exist
        def env = System.getenv()
        def binDir = new File(env.get("HOME") + "/bin")
        if (!binDir.exists()) {
            binDir.mkdir()
        }

        // copy jar to $HOME/bin directory
        def from = jar.archiveFile.get().asFile.toPath()
        def to = binDir.toString() + "/" + jar.archiveFile.get().asFile.getName()
        Files.copy(from, new FileOutputStream(to))

        // delete old alias from ~/.bashrc
        def deletePattern = "alias pungl*"
        def deleteOldAlias = new ProcessBuilder().command("bash", "-c", "sed -i \'/$deletePattern/d\' ~/.bashrc")
                .start()
                .waitFor()

        // add new alias to ~/.bashrc
        def alias = "alias pungl=\\\"java -jar $to\\\""
        def addNewAlias = new ProcessBuilder().command("bash", "-c", "echo $alias >> ~/.bashrc")
                .start()
                .waitFor()
    }
}
